# ERC-165


이더리움은 다양한 표준이 존재하고 각 표준은 인터페이스 형태로 정의되어 있습니다. 표준들이 많다보니 버전과 스마트컨트랙트가 구현한 표준 종류들을 구분할 수 있는 수단들이 필요하게 되었습니다.

그러나 스마트컨트랙트 주소만으로는 각 표준이 되는 인터페이스에 맞춰 컨트랙트가 구현이 되었는지 또는 몇 버전의 표준으로 구현했는지는 알 수 없기에 ERC-165이 도입되었습니다.


## Openzepplin `ERC165`

- **ERC165.sol**
```sol
pragma solidity ^0.8.0;

import "./IERC165.sol";

abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}
```

- **IERC165.sol**

```sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
```

`solidity`의 함수들은 모두 selector를 가지고 있습니다. selector는 쉽게 생각하면 함수의 아이디이며, 내부적으로는 함수의 signature를 해싱한 값입니다.

실제 구현은 interface의 각 함수들의 selector들을 ^(xor)연산을 하여 구합니다.


```sol
pragma solidity ^0.4.20;

interface Solidity101 {
    function hello() external pure;
    function world(int) external pure;
}

contract Selector {
    function calculateSelector() public pure returns (bytes4) {
        Solidity101 i;
        return i.hello.selector ^ i.world.selector;
    }
}

```

해싱한 값은 해싱한 값을 비트단위의 xor연산을 하여 구합니다. 

이 selector를 체킹하여 impl 구현체가 interface를 support하는지 체크할 수 있습니다. 이 기능을 `supportsInterface`를 구현함으로써 가능하며 아래는 openzeppelin의 ERC721.sol의 impl일부입니다.


```sol
... 중략 ...

contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    ... 중략 ...

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }
```

코드를 보면 openzepplin의 `ERC721`를 구현했는지 여부는 전달받은 interfaceId라는 함수 시그니처 해싱들을 xor하여 구해진 interfaceId들을 비교하는 것이며, 검사 기준은 IERC721 또는 IERC721Metadata를 구현하였는지 또는 ERC165의 interface를 구현하였는지를 통해서 bool 값을 return합니다.